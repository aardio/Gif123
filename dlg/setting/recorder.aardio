import config;
import uiLanguage;
import style;
import fonts.FontAwesomeGif123;
import mouse.highlighter;
import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=449;bottom=435;bgcolor=16777215;border="none";exmode="none";max=false;min=false;mode="popup")
winform.add(
btnPointerSetting={cls="plus";text='\uF013';left=368;top=210;right=393;bottom=235;border={radius=6};clip=1;color=9868950;font=LOGFONT(name='FontAwesomeGif123');hide=1;notify=1;z=24};
btnSave={cls="plus";text="Save";left=262;top=385;right=361;bottom=413;align="left";bgcolor=-5197169;disabled=1;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(name='FontAwesomeGif123');padding={left=12}};iconText='\uF00C';notify=1;textPadding={left=32};z=1};
chkCaptureMouse={cls="plus";text="Capture mouse cursor while recording";left=40;top=175;right=388;bottom=205;align="left";dl=1;dt=1;font=LOGFONT(h=-14);iconStyle={align="left";font=LOGFONT(h=-14;name='FontAwesomeGif123');padding={left=2}};iconText='\uF0C8 ';notify=1;textPadding={left=21};z=21};
chkCountdownBeforeRecording={cls="plus";text="Show countdown before recording";left=40;top=259;right=388;bottom=289;align="left";dl=1;dt=1;font=LOGFONT(h=-14);iconStyle={align="left";font=LOGFONT(h=-14;name='FontAwesomeGif123');padding={left=2}};iconText='\uF0C8 ';notify=1;textPadding={left=21};z=28};
chkHighlightMouse={cls="plus";text="Highlight mouse cursor while recording";left=40;top=18;right=388;bottom=48;align="left";dl=1;dt=1;font=LOGFONT(h=-14);iconStyle={align="left";font=LOGFONT(h=-14;name='FontAwesomeGif123');padding={left=2}};iconText='\uF0C8 ';notify=1;textPadding={left=21};z=2};
editColor={cls="plus";text="#66880055";left=137;top=113;right=308;bottom=139;align="left";border={bottom=1;color=-6908266};editable=1;font=LOGFONT(h=-13);textPadding={top=6;bottom=2};z=9};
hotkey={cls="hotkey";left=227;top=344;right=411;bottom=364;edge=1;z=25};
lbCountdownValue={cls="plus";text="0 seconds ";left=321;top=302;right=438;bottom=319;align="left";color=7895160;dl=1;dt=1;notify=1;z=29};
lbHighlightColor={cls="plus";text="Color：";left=51;top=127;right=130;bottom=144;align="right";color=7895160;dl=1;dt=1;notify=1;z=10};
lbHighlightPadding={cls="plus";text="Padding：";left=51;top=89;right=130;bottom=106;align="right";color=7895160;dl=1;dt=1;notify=1;z=7};
lbHighlightPaddingValue={cls="plus";text="12";left=320;top=92;right=375;bottom=109;align="left";color=7895160;dl=1;dt=1;notify=1;z=8};
lbHighlightSize={cls="plus";text="Size：";left=51;top=58;right=130;bottom=75;align="right";color=7895160;dl=1;dt=1;notify=1;z=3};
lbHighlightSizeValue={cls="plus";text="12";left=320;top=62;right=373;bottom=79;align="left";color=7895160;dl=1;dt=1;notify=1;z=4};
lbHkRecord={cls="plus";text="Start/Stop Recording hotkey：";left=4;top=344;right=224;bottom=364;align="right";dl=1;dt=1;notify=1;z=26};
lbPointerSize={cls="plus";text="Pointer size：";left=15;top=211;right=130;bottom=231;align="right";color=7895160;dl=1;dt=1;notify=1;z=27};
lbPointerSizeValue={cls="plus";text="100%";left=320;top=214;right=359;bottom=231;align="left";color=7895160;dl=1;dt=1;notify=1;z=22};
palette1={cls="plus";left=137;top=150;right=152;bottom=162;dr=1;dt=1;edge=1;z=11};
palette10={cls="plus";left=293;top=150;right=308;bottom=162;dr=1;dt=1;edge=1;z=20};
palette2={cls="plus";left=154;top=150;right=169;bottom=162;dr=1;dt=1;edge=1;z=12};
palette3={cls="plus";left=172;top=150;right=187;bottom=162;dr=1;dt=1;edge=1;z=13};
palette4={cls="plus";left=189;top=150;right=204;bottom=162;dr=1;dt=1;edge=1;z=14};
palette5={cls="plus";left=206;top=150;right=221;bottom=162;dr=1;dt=1;edge=1;z=15};
palette6={cls="plus";left=224;top=150;right=239;bottom=162;dr=1;dt=1;edge=1;z=16};
palette7={cls="plus";left=241;top=150;right=256;bottom=162;dr=1;dt=1;edge=1;z=17};
palette8={cls="plus";left=258;top=150;right=273;bottom=162;dr=1;dt=1;edge=1;z=18};
palette9={cls="plus";left=276;top=150;right=291;bottom=162;dr=1;dt=1;edge=1;z=19};
tbCountdown={cls="plus";left=136;top=302;right=310;bottom=315;bgcolor=-2512093;border={radius=-1};color=1995263;dl=1;dt=1;foreRight=13;forecolor=-14911489;notify=1;paddingBottom=5;paddingTop=5;z=30};
tbHighlighterPadding={cls="plus";left=135;top=91;right=309;bottom=104;bgcolor=-2512093;border={radius=-1};color=1995263;dl=1;dt=1;foreRight=13;forecolor=-14911489;paddingBottom=5;paddingTop=5;z=5};
tbHighlighterSize={cls="plus";left=135;top=62;right=309;bottom=75;bgcolor=-2512093;border={radius=-1};color=1995263;dl=1;dt=1;foreRight=13;forecolor=-14911489;notify=1;paddingBottom=5;paddingTop=5;z=6};
tbPointerSize={cls="plus";left=135;top=214;right=309;bottom=227;bgcolor=-2512093;border={radius=-1};color=1995263;dl=1;dt=1;foreRight=13;forecolor=-14911489;notify=1;paddingBottom=5;paddingTop=5;z=23}
)
/*}}*/

winform.btnSave.skin(style.defaultButton); 
winform.tbHighlighterSize.setTrackbarRange(32,256);
winform.tbHighlighterPadding.setTrackbarRange(6,128);
winform.tbHighlighterSize.skin(style.trackbar);
winform.tbHighlighterPadding.skin(style.trackbar)
winform.tbPointerSize.setTrackbarRange(100,200);;
winform.tbPointerSize.skin(style.trackbar);
winform.chkCaptureMouse.skin(style.checkbox);
winform.chkHighlightMouse.skin(style.checkbox);
winform.chkCountdownBeforeRecording.skin(style.checkbox);
winform.tbCountdown.setTrackbarRange(0,10);
winform.tbCountdown.skin(style.trackbar);

winform.hotkey.sethotkey(config.setting.recordHotkey[1],config.setting.recordHotkey[2])	
winform.chkHighlightMouse.checked = config.setting.chkHighlight;
winform.tbHighlighterSize.progressPos = config.setting.highlightSize;
winform.lbHighlightSizeValue.text = config.setting.highlightSize; 
winform.tbHighlighterPadding.progressPos = config.setting.highlightPadding;
winform.lbHighlightPaddingValue.text = config.setting.highlightPadding; 
winform.editColor.text = gdi.colorStringify(config.setting.highlightColor,true);
var highlightColorPalette = config.setting.highlightColorPalette;
for(i=1;10;1){
	winform["palette"+i].skin(style.palette);
	winform["palette"+i].background = highlightColorPalette[i];
	winform["palette"+i].oncommand = function(){
		winform.editColor.text = gdi.colorStringify(owner.backgroundColor,true);
	}
}

winform.tbHighlighterSize.onPosChanged = function( pos,thumbTrack ){
	winform.lbHighlightSizeValue.text = pos;
	winform.btnSave.disabled = false;
	winform.chkHighlightMouse.oncommand();
}

winform.tbHighlighterPadding.onPosChanged = function( pos,thumbTrack ){
	winform.lbHighlightPaddingValue.text = pos;
	winform.btnSave.disabled = false;
	winform.chkHighlightMouse.oncommand();
}

winform.editColor.editBox.onChange = function(){ 
	winform.btnSave.disabled = false;
	if(string.match(owner.text,"#\x\x\x\x\x\x\x\x")){
		winform.btnSave.disabled = false;
		winform.chkHighlightMouse.oncommand();
	}
}

winform.chkHighlightMouse.oncommand = function(id,event){
	if(winform.chkHighlightMouse.checked){
		if(!winform.testHighlighter){
			winform.testHighlighter = mouse.highlighter(winform);
		}
		
		winform.testHighlighter.changeConfig(
			gdi.colorParse(winform.editColor.text), 
			winform.tbHighlighterSize.progressPos, 
			winform.tbHighlighterPadding.progressPos
		);
		
		winform.testHighlighter.show();
	}
	else {
		if(winform.testHighlighter){
			winform.testHighlighter.close();
			winform.testHighlighter = null;
		}
	}
	
	winform.btnSave.disabled = false;
}

winform.tbPointerSize.progressPos = config.setting.pointerSize;
winform.lbPointerSizeValue.text  = config.setting.pointerSize + "%";
winform.chkCaptureMouse.checked = config.setting.captureMouse;

winform.tbPointerSize.onPosChanged = function( pos,thumbTrack ){
	winform.btnSave.disabled = false;	
	winform.lbPointerSizeValue.text = pos + "%";
	if(_WIN10_LATER){
		::User32.SystemParametersInfo(0x2029, 0, 32 * (pos/100), 1);
	}
}

winform.chkCaptureMouse.oncommand = function(id,event){
	winform.btnSave.disabled = false;
}

if(_WIN10_LATER){
	winform.btnPointerSetting.show(true);
	winform.btnPointerSetting.skin({
		background={
			active=0xFF0078B0;
			hover=0xFF00AEFF;
		}; 
	})
	
	winform.btnPointerSetting.oncommand = function(id,event){
		import process.control;
		process.control("ms-settings:easeofaccess-mousepointer")
	}
}


winform.btnSave.oncommand = function(id,event){
	
	config.setting.countdown = winform.tbCountdown.progressPos;
	config.setting.chkHighlight = winform.chkHighlightMouse.checked;
	config.setting.highlightSize = winform.tbHighlighterSize.progressPos;
	config.setting.highlightPadding = winform.tbHighlighterPadding.progressPos;
	config.setting.highlightColor = gdi.colorParse(winform.editColor.text); 

	config.setting.captureMouse = winform.chkCaptureMouse.checked;
	config.setting.pointerSize = winform.tbPointerSize.progressPos;
	
	config.setting.save();	
	
	winform.btnSave.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250';text=''};
	win.delay(500);
	winform.btnSave.disabledText = null;
	
	var hotmod,hotkey = winform.hotkey.gethotkey()
	config.setting.recordHotkey = {hotmod;hotkey};
	publish("spellingHotkeyChange");
	
	winform.btnSave.disabled = true;	
}

winform.hotkey.oncommand = function(id,event){
	winform.btnSave.disabled = false;
}

winform.tbCountdown.progressPos = config.setting.countdown;
if(config.setting.countdown){
	winform.chkCountdownBeforeRecording.checked = true;	
}

winform.chkCountdownBeforeRecording.oncommand = function(id,event){
	if(owner.checked){
		if(!winform.tbCountdown.progressPos){
			winform.tbCountdown.progressPos = 3;
		}
	}
	else {
		winform.tbCountdown.progressPos = 0;
	}
	
	winform.btnSave.disabled = false;
}

winform.tbCountdown.onPosChanged = function( pos,thumbTrack ){
	winform.chkCountdownBeforeRecording.checked = !!pos;
	winform.lbCountdownValue.text = pos + " " + uiLanguage.seconds;
	winform.btnSave.disabled = false;	
}

winform.btnSave.text = uiLanguage.save; 
winform.chkHighlightMouse.text  = uiLanguage.highlightMouse;
winform.lbHighlightSize.text  = uiLanguage.highlightSize;
winform.lbHighlightPadding.text  = uiLanguage.highlightPadding;
winform.lbHighlightColor.text  = uiLanguage.highlightColor; 
winform.chkCaptureMouse.text = uiLanguage.captureCursor;
winform.lbPointerSize.text = uiLanguage.changePointerSize;
winform.lbHkRecord.text = uiLanguage.hkRecord;
winform.chkCountdownBeforeRecording.text = uiLanguage.countdown; 
winform.lbCountdownValue.text = config.setting.countdown + " " + uiLanguage.seconds;

if(!winform.parent){
	import win.ui.simpleWindow;
	win.ui.simpleWindow(winform);	
}

winform.onDestroy = function(){
    if(winform.testHighlighter){
    	winform.testHighlighter.close();
    }
}

winform.btnSave.disabled = true;
winform.show();
win.loopMessage();
return winform;